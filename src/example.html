<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Shadows and sprites</title>

    <link rel="stylesheet" href="style.css">
    <script src="https://threejs.org/build/three.js"></script>
    <script src="https://threejs.org/examples/js/controls/OrbitControls.js"></script>

    <script id="vertexShader" type="x-shader/x-vertex">
        float INSTANCED_SPRITE_OVERSCALE = 1.3;

        #ifdef INSTANCED_POS
            attribute vec4 offset;
            #ifdef SPHERE_SPRITE
                varying vec4 instOffset;
                varying vec4 spritePosEye;
            #else
                varying vec3 vNormal;
            #endif
        #endif

        attribute vec3 color;
        varying vec3 vColor;

        varying vec3 vViewPosition;

        void main()	{
            vColor = color;

            #ifdef INSTANCED_POS
                #ifdef SPHERE_SPRITE
                    instOffset = offset;

                    vec4 posEye = modelViewMatrix * vec4( offset.xyz, 1.0 );
                    float scale = length(modelViewMatrix[0]);
                    vec4 mvPosition = posEye + vec4( position.xyz * offset.w * scale * INSTANCED_SPRITE_OVERSCALE, 0.0 );
                    posEye.w = offset.w * scale;

                    spritePosEye = posEye;
                #else
                    vec3 transformedNormal = normalMatrix * normal;
                    vNormal = normalize(transformedNormal);

                    vec4 localPos = vec4( offset.xyz + position.xyz * offset.w, 1.0 );
                    vec4 mvPosition = modelViewMatrix * localPos;
                #endif
            #endif

            vViewPosition = - mvPosition.xyz;
            gl_Position = projectionMatrix * mvPosition;
        }
    </script>

    <script id="fragmentShader" type="x-shader/x-fragment">
        #define PI 3.14159265359
        #define RECIPROCAL_PI 0.31830988618

        varying vec3 vColor;

        varying vec3 vViewPosition;

        uniform mat4 modelMatrix;
        uniform mat4 modelViewMatrix;
        uniform mat4 invModelViewMatrix;
        uniform mat3 normalMatrix;

        uniform vec3 diffuse;
        uniform vec3 specular;
        uniform float shininess;

        #ifdef SPHERE_SPRITE
            varying vec4 instOffset;
            varying vec4 spritePosEye;
        #else
            varying vec3 vNormal;
        #endif

        #if defined(SPHERE_SPRITE)
            vec4 pixelPosEye;
            uniform float zOffset;
            uniform mat4 projectionMatrix;

            float calcDepthForSprites(vec4 pixelPosEye, float zOffset, mat4 projMatrix) {
                vec4 pixelPosScreen = projMatrix * pixelPosEye;
                return 0.5 * (pixelPosScreen.z / pixelPosScreen.w + 1.0) + zOffset;
            }
            float intersect_ray_sphere(in vec3 origin, in vec3 ray, out vec3 point) {
                // intersect XZ-projected ray with circle
                float a = dot(ray, ray);
                float b = dot(ray, origin);
                float c = dot(origin, origin) - 1.0;
                float det = b * b - a * c;
                if (det < 0.0) return -1.0;
                float t1 = (-b - sqrt(det)) / a;
                float t2 = (-b + sqrt(det)) / a;

                // calculate both intersection points
                vec3 p1 = origin + ray * t1;
                vec3 p2 = origin + ray * t2;

                // choose nearest point
                if (t1 >= 0.0) {
                    point = p1;
                    return t1;
                }
                if (t2 >= 0.0) {
                    point = p2;
                    return t2;
                }

                return -1.0;
            }

            float get_sphere_point(in vec3 pixelPosEye, out vec3 point) {
                // transform camera pos into sphere local coords
                vec4 v = invModelViewMatrix * vec4(0.0, 0.0, 0.0, 1.0);
                vec3 origin = (v.xyz - instOffset.xyz) / instOffset.w;

                // transform (camera -> pixel) ray into cylinder local coords
                v = invModelViewMatrix * vec4(pixelPosEye, 0.0);
                vec3 ray = normalize(v.xyz);

                return intersect_ray_sphere(origin, ray, point);
            }
        #endif

        #ifdef USE_LIGHTS
            struct ReflectedLight {
                vec3 directDiffuse;
                vec3 directSpecular;
                vec3 indirectDiffuse;
            };

            struct BlinnPhongMaterial {
                vec3  diffuseColor;
                vec3  specularColor;
                float specularShininess;
            };

            struct GeometricContext {
                vec3 normal;
                vec3 viewDir;
            };

            struct DirectionalLight {
                vec3 direction;
                vec3 color;
            };

            uniform DirectionalLight directionalLights[ NUM_DIR_LIGHTS ];
            uniform vec3 ambientLightColor;

            vec3 BRDF_Diffuse_Lambert( const in vec3 diffuseColor ) {
                return RECIPROCAL_PI * diffuseColor;
            }

            vec3 F_Schlick( const in vec3 specularColor, const in float dotLH ) {
                float fresnel = exp2( ( -5.55473 * dotLH - 6.98316 ) * dotLH );
                return ( 1.0 - specularColor ) * fresnel + specularColor;
            }

            float D_BlinnPhong( const in float shininess, const in float dotNH ) {
                return RECIPROCAL_PI * ( shininess * 0.5 + 1.0 ) * pow( dotNH, shininess );
            }

            vec3 BRDF_Specular_BlinnPhong( const in DirectionalLight incidentLight, const in GeometricContext geometry, const in vec3 specularColor, const in float shininess ) {
                vec3 halfDir = normalize( incidentLight.direction + geometry.viewDir );
                float dotNH = saturate(dot( geometry.normal, halfDir ));
                float dotLH = saturate(dot( incidentLight.direction, halfDir ));

                vec3 F = F_Schlick( specularColor, dotLH );
                float G = 0.25;
                float D = D_BlinnPhong( shininess, dotNH );

                return F * ( G * D );
            }

            void RE_Direct_BlinnPhong( const in DirectionalLight directLight, const in GeometricContext geometry, const in BlinnPhongMaterial material, inout ReflectedLight reflectedLight ) {

                float dotNL = saturate( dot( geometry.normal, directLight.direction ));

                vec3 irradiance = dotNL * directLight.color * PI;
                reflectedLight.directDiffuse += irradiance * BRDF_Diffuse_Lambert( material.diffuseColor );
                reflectedLight.directSpecular += irradiance * BRDF_Specular_BlinnPhong( directLight, geometry, material.specularColor, material.specularShininess );
            }

            void RE_IndirectDiffuse_BlinnPhong( const in vec3 irradiance, const in BlinnPhongMaterial material, inout ReflectedLight reflectedLight ) {
                reflectedLight.indirectDiffuse += irradiance * BRDF_Diffuse_Lambert( material.diffuseColor );
            }

            vec3 calcLighting(const in GeometricContext geometry, const in BlinnPhongMaterial material, vec3 vViewPosition) {
                ReflectedLight reflectedLight = ReflectedLight( vec3( 0.0 ), vec3( 0.0 ), vec3( 0.0 ));
                vec3 irradiance = ambientLightColor * PI;

                for ( int i = 0; i < NUM_DIR_LIGHTS; i ++ ) {
                    RE_Direct_BlinnPhong( directionalLights[i], geometry, material, reflectedLight);
                }

                RE_IndirectDiffuse_BlinnPhong(irradiance, material, reflectedLight);

                return saturate(reflectedLight.indirectDiffuse + reflectedLight.directDiffuse + reflectedLight.directSpecular);
            }
        #endif

        vec3 viewNormalSprites;
        vec3 normal;

        const float PackUpscale = 256. / 255.; // fraction -> 0..1 (including 1)
        const float UnpackDownscale = 255. / 256.; // 0..1 -> fraction (excluding 1)

        const vec3 PackFactors = vec3( 256. * 256. * 256., 256. * 256.,  256. );
        const vec4 UnpackFactors = UnpackDownscale / vec4( PackFactors, 1. );


        const float ShiftRight8 = 1. / 256.;

        vec4 packDepthToRGBA( const in float v ) {
            vec4 r = vec4( fract( v * PackFactors ), v );
            r.yzw -= r.xyz * ShiftRight8; // tidy overflow
            return r * PackUpscale;
        }

        float unpackRGBAToDepth( const in vec4 v ) {
            return dot( v, UnpackFactors );
        }

        void main()	{
            // ray-trace sphere surface
            #if defined(SPHERE_SPRITE)
                vec3 p;
                float d = get_sphere_point(-vViewPosition, p);
                #ifdef BUILD_SHADOWS
                    #if defined (USE_DISCARD_IN_SHADOWS)
                        if (d == -1.0) discard;
                    #endif
                #else
                     if (d == -1.0) discard;
                #endif
                pixelPosEye = vec4(spritePosEye.xyz, 1.0);
                pixelPosEye.z += instOffset.w *
                (modelViewMatrix[0][2] * p.x +
                modelViewMatrix[1][2] * p.y +
                modelViewMatrix[2][2] * p.z);
                normal = normalize(normalMatrix * p);

                //gl_FragDepthEXT = calcDepthForSprites(pixelPosEye, zOffset, projectionMatrix);
            #else
                normal = vNormal;
            #endif

            vec3 diffuseColor = diffuse * vColor;

            #ifdef USE_LIGHTS
                GeometricContext geometry = GeometricContext(normal, normalize( vViewPosition ));
                BlinnPhongMaterial material = BlinnPhongMaterial(diffuseColor, specular, shininess);
                vec3 outgoingLight = calcLighting(geometry, material, vViewPosition);
            #else
                vec3 outgoingLight = diffuseColor;
            #endif

            #ifdef COLOR_FROM_DEPTH
                float depth = 0.0;
                #if defined(SPHERE_SPRITE)
                    gl_FragDepthEXT = calcDepthForSprites(pixelPosEye, zOffset, projectionMatrix);
                    depth = gl_FragDepthEXT;
                #else
                    depth = gl_FragCoord.z;
                #endif
                gl_FragColor = packDepthToRGBA(depth);
                return;
            #endif
            gl_FragColor = vec4(outgoingLight, 0.8);
        }
    </script>
</head>
<body>
<script src="example.js"></script>
<!-- <div class="miew-container" style="width:640px; height:480px"></div> -->
</body>
</html>